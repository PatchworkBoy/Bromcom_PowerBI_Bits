// getSignInAppapi
//
// Refer to https://signinapp.com/docs/client-api/client-setup.html#go-to-manage-api-client for generating relevant keys.
//
// PublicKey = client public key
// SecretKey = api secret key
// Endpoint = /groups (for example)
//
// See https://signinapp.com/docs/client-api/resource-endpoints.html

(PublicKey as text, SecretKey as text, endpoint as text) => let
    BasicAuth = PublicKey & ":" & SecretKey,
    Base64_Auth = Binary.ToText(Text.ToBinary(BasicAuth), BinaryEncoding.Base64),
    BaseUrl = "https://backend.signinapp.com/client-api/v1",
    RelativePath = endpoint,
    Headers = [
        #"Content-Type"="application/json",
        Accept="application/json",
        Authorization = "Basic " & Base64_Auth
    ],
    Options = [
        Headers = Headers,
        RelativePath = RelativePath,
        ManualStatusHandling = {400, 404}
    ],
    binary_response = Web.Contents(BaseUrl, Options),
    buffered = Binary.Buffer(binary_response),
    response_metadata = Value.Metadata(binary_response),
    status_code = response_metadata[Response.Status],
    // for 400 and 404, return extra error info
    maybe_bytes =
        if List.Contains({400, 404}, status_code)
        then response_metadata else buffered, 
    // returns either document else web request error
    result = Json.Document(maybe_bytes)
in
    result
